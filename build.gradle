/*
 * The MIT License (MIT)
 * Copyright (c) 2017 Sybit GmbH
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 */

plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

group = 'com.tryadhawk'
version = '2.0.0'

description = """com.tryadhawk airtable"""

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
    annotationProcessor 'com.google.auto.value:auto-value:1.7'
    api 'com.google.auto.value:auto-value-annotations:1.7'
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api group: 'com.mashape.unirest', name: 'unirest-java', version:'1.4.9'
    api group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.9'
    api group: 'org.json', name: 'json', version:'20160810'
    api group: 'com.google.code.gson', name: 'gson', version:'2.8.5'
    api group: 'commons-beanutils', name: 'commons-beanutils', version:'1.9.3'
    api group: 'commons-io', name: 'commons-io', version:'2.6'
    api group: 'org.slf4j', name: 'slf4j-api', version:'1.7.26'

    testImplementation group: 'junit', name: 'junit', version:'4.12'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version:'2.23.2'
    testImplementation group: 'org.slf4j', name: 'slf4j-jdk14', version:'1.7.26'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/adhawk/airtable-java")
            credentials {
                username = System.getenv("REPO_USERNAME")
                password = System.getenv("REPO_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId rootProject.name
            from components.java
        }
    }
}
